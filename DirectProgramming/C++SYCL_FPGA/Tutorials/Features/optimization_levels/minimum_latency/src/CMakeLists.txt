set(SOURCE_FILE minimum_latency.cpp)
set(TARGET_NAME_NO_CONTROL no_control)
set(TARGET_NAME_MINIMUM_LATENCY minimum_latency)
set(EMULATOR_TARGET_NO_CONTROL ${TARGET_NAME_NO_CONTROL}.fpga_emu)
set(SIMULATOR_TARGET_NO_CONTROL ${TARGET_NAME_NO_CONTROL}.fpga_sim)
set(SIMULATOR_TARGET_MINIMUM_LATENCY ${TARGET_NAME_MINIMUM_LATENCY}.fpga_sim)
set(FPGA_EARLY_IMAGE_NO_CONTROL ${TARGET_NAME_NO_CONTROL}_report.a)
set(FPGA_EARLY_IMAGE_MINIMUM_LATENCY ${TARGET_NAME_MINIMUM_LATENCY}_report.a)
set(FPGA_TARGET_NO_CONTROL ${TARGET_NAME_NO_CONTROL}.fpga)
set(FPGA_TARGET_MINIMUM_LATENCY ${TARGET_NAME_MINIMUM_LATENCY}.fpga)

# FPGA board selection
if(NOT DEFINED FPGA_DEVICE)
    set(FPGA_DEVICE "intel_a10gx_pac:pac_a10")
    message(STATUS "FPGA_DEVICE was not specified.\
                    \nConfiguring the design to run on the default FPGA board ${FPGA_DEVICE} (Intel(R) PAC with Intel Arria(R) 10 GX FPGA). \
                    \nPlease refer to the README for information on board selection.")
else()
    message(STATUS "Configuring the design to run on FPGA board ${FPGA_DEVICE}")
endif()

# This is a Windows-specific flag that enables exception handling in host code
if(WIN32)
    set(WIN_FLAG "/EHsc")
endif()

# A SYCL ahead-of-time (AoT) compile processes the device code in two stages.
# 1. The "compile" stage compiles the device code to an intermediate representation (SPIR-V).
# 2. The "link" stage invokes the compiler's FPGA backend before linking.
#    For this reason, FPGA backend flags must be passed as link flags in CMake.
set(EMULATOR_COMPILE_FLAGS "-Wall ${WIN_FLAG} -fsycl -fintelfpga -DFPGA_EMULATOR")
set(EMULATOR_LINK_FLAGS "-fsycl -fintelfpga")
set(SIMULATOR_COMPILE_FLAGS "-Wall ${WIN_FLAG} -fsycl -fintelfpga -DFPGA_SIMULATOR")
set(SIMULATOR_LINK_FLAGS "-fsycl -fintelfpga -Xssimulation -Xstarget=${FPGA_DEVICE} ${USER_HARDWARE_FLAGS}")
set(HARDWARE_COMPILE_FLAGS "-Wall ${WIN_FLAG} -fsycl -fintelfpga")
set(HARDWARE_LINK_FLAGS "-fsycl -fintelfpga -Xshardware -Xstarget=${FPGA_DEVICE} ${USER_HARDWARE_FLAGS}")
# use cmake -D USER_HARDWARE_FLAGS=<flags> to set extra flags for FPGA backend compilation

###############################################################################
### FPGA Emulator
###############################################################################
# To compile in a single command:
#    icpx -fsycl -fintelfpga -DFPGA_EMULATOR minimum_latency.cpp -o no_control.fpga_emu
# CMake executes:
#    [compile] icpx -fsycl -fintelfpga -DFPGA_EMULATOR -o no_control.cpp.o -c minimum_latency.cpp
#    [link]    icpx -fsycl -fintelfpga no_control.cpp.o -o no_control.fpga_emu
add_executable(${EMULATOR_TARGET_NO_CONTROL} ${SOURCE_FILE})
target_include_directories(${EMULATOR_TARGET_NO_CONTROL} PRIVATE ../../../../../include)
set_target_properties(${EMULATOR_TARGET_NO_CONTROL} PROPERTIES COMPILE_FLAGS "${EMULATOR_COMPILE_FLAGS}")
set_target_properties(${EMULATOR_TARGET_NO_CONTROL} PROPERTIES LINK_FLAGS "${EMULATOR_LINK_FLAGS}")
add_custom_target(fpga_emu DEPENDS ${EMULATOR_TARGET_NO_CONTROL})

###############################################################################
### Generate Report
###############################################################################
# To compile manually:
#   icpx -fsycl -fintelfpga -Xshardware -Xstarget=<FPGA_DEVICE> -fsycl-link=early minimum_latency.cpp -o no_control.a
#   icpx -fsycl -fintelfpga -Xshardware -Xstarget=<FPGA_DEVICE> -fsycl-link=early minimum_latency.cpp -Xsoptimize=latency -o minimum_latency.a
# The compile output is not an executable, but an intermediate compilation result unique to SYCL.
add_executable(${FPGA_EARLY_IMAGE_NO_CONTROL} ${SOURCE_FILE})
target_include_directories(${FPGA_EARLY_IMAGE_NO_CONTROL} PRIVATE ../../../../../include)
add_executable(${FPGA_EARLY_IMAGE_MINIMUM_LATENCY} ${SOURCE_FILE})
target_include_directories(${FPGA_EARLY_IMAGE_MINIMUM_LATENCY} PRIVATE ../../../../../include)
add_custom_target(report DEPENDS ${FPGA_EARLY_IMAGE_NO_CONTROL} ${FPGA_EARLY_IMAGE_MINIMUM_LATENCY})
set_target_properties(${FPGA_EARLY_IMAGE_NO_CONTROL} PROPERTIES COMPILE_FLAGS "${HARDWARE_COMPILE_FLAGS}")
set_target_properties(${FPGA_EARLY_IMAGE_NO_CONTROL} PROPERTIES LINK_FLAGS "${HARDWARE_LINK_FLAGS} -fsycl-link=early")
set_target_properties(${FPGA_EARLY_IMAGE_MINIMUM_LATENCY} PROPERTIES COMPILE_FLAGS "${HARDWARE_COMPILE_FLAGS}")
set_target_properties(${FPGA_EARLY_IMAGE_MINIMUM_LATENCY} PROPERTIES LINK_FLAGS "${HARDWARE_LINK_FLAGS} -fsycl-link=early -Xsoptimize=latency")
# fsycl-link=early stops the compiler after RTL generation, before invoking QuartusÂ®

###############################################################################
### FPGA Simulator
###############################################################################
# To compile in a single command:
#    icpx -fsycl -fintelfpga -Xssimulation -Xstarget=<FPGA_DEVICE> -DFPGA_SIMULATOR minimum_latency.cpp -o no_control.fpga_sim
#    icpx -fsycl -fintelfpga -Xssimulation -Xstarget=<FPGA_DEVICE> -DFPGA_SIMULATOR minimum_latency.cpp -Xsoptimize=latency -o minimum_latency.fpga_sim
# CMake executes:
#    [compile] icpx -fsycl -fintelfpga -DFPGA_SIMULATOR -o no_control.cpp.o -c minimum_latency.cpp
#    [link]    icpx -fsycl -fintelfpga -Xssimulation -Xstarget=<FPGA_DEVICE> no_control.cpp.o -o no_control.fpga_sim
#    [compile] icpx -fsycl -fintelfpga -DFPGA_SIMULATOR -o minimum_latency.cpp.o -c minimum_latency.cpp
#    [link]    icpx -fsycl -fintelfpga -Xssimulation -Xstarget=<FPGA_DEVICE> minimum_latency.cpp.o -Xsoptimize=latency -o minimum_latency.fpga_sim
add_executable(${SIMULATOR_TARGET_NO_CONTROL} ${SOURCE_FILE})
target_include_directories(${SIMULATOR_TARGET_NO_CONTROL} PRIVATE ../../../../../include)
add_executable(${SIMULATOR_TARGET_MINIMUM_LATENCY} ${SOURCE_FILE})
target_include_directories(${SIMULATOR_TARGET_MINIMUM_LATENCY} PRIVATE ../../../../../include)
add_custom_target(fpga_sim DEPENDS ${SIMULATOR_TARGET_NO_CONTROL} ${SIMULATOR_TARGET_MINIMUM_LATENCY})
set_target_properties(${SIMULATOR_TARGET_NO_CONTROL} PROPERTIES COMPILE_FLAGS "${SIMULATOR_COMPILE_FLAGS}")
set_target_properties(${SIMULATOR_TARGET_NO_CONTROL} PROPERTIES LINK_FLAGS "${SIMULATOR_LINK_FLAGS}")
set_target_properties(${SIMULATOR_TARGET_MINIMUM_LATENCY} PROPERTIES COMPILE_FLAGS "${SIMULATOR_COMPILE_FLAGS}")
set_target_properties(${SIMULATOR_TARGET_MINIMUM_LATENCY} PROPERTIES LINK_FLAGS "${SIMULATOR_LINK_FLAGS} -Xsoptimize=latency")

###############################################################################
### FPGA Hardware
###############################################################################
# To compile in a single command:
#   icpx -fsycl -fintelfpga -Xshardware -Xstarget=<FPGA_DEVICE> minimum_latency.cpp -o no_control.fpga
#   icpx -fsycl -fintelfpga -Xshardware -Xstarget=<FPGA_DEVICE> minimum_latency.cpp -Xsoptimize=latency -o minimum_latency.fpga
# CMake executes:
#   [compile] icpx -fsycl -fintelfpga -o no_control.cpp.o -c minimum_latency.cpp
#   [link]    icpx -fsycl -fintelfpga -Xshardware -Xstarget=<FPGA_DEVICE> no_control.cpp.o -o no_control.fpga
#   [compile] icpx -fsycl -fintelfpga -o minimum_latency.cpp.o -c minimum_latency.cpp
#   [link]    icpx -fsycl -fintelfpga -Xshardware -Xstarget=<FPGA_DEVICE> minimum_latency.cpp.o -Xsoptimize=latency -o minimum_latency.fpga
add_executable(${FPGA_TARGET_NO_CONTROL} EXCLUDE_FROM_ALL ${SOURCE_FILE})
target_include_directories(${FPGA_TARGET_NO_CONTROL} PRIVATE ../../../../../include)
add_executable(${FPGA_TARGET_MINIMUM_LATENCY} EXCLUDE_FROM_ALL ${SOURCE_FILE})
target_include_directories(${FPGA_TARGET_MINIMUM_LATENCY} PRIVATE ../../../../../include)
add_custom_target(fpga DEPENDS ${FPGA_TARGET_NO_CONTROL} ${FPGA_TARGET_MINIMUM_LATENCY})
set_target_properties(${FPGA_TARGET_NO_CONTROL} PROPERTIES COMPILE_FLAGS "${HARDWARE_COMPILE_FLAGS}")
set_target_properties(${FPGA_TARGET_NO_CONTROL} PROPERTIES LINK_FLAGS "${HARDWARE_LINK_FLAGS} -reuse-exe=${CMAKE_BINARY_DIR}/${FPGA_TARGET_NO_CONTROL}")
set_target_properties(${FPGA_TARGET_MINIMUM_LATENCY} PROPERTIES COMPILE_FLAGS "${HARDWARE_COMPILE_FLAGS}")
set_target_properties(${FPGA_TARGET_MINIMUM_LATENCY} PROPERTIES LINK_FLAGS "${HARDWARE_LINK_FLAGS} -reuse-exe=${CMAKE_BINARY_DIR}/${FPGA_TARGET_MINIMUM_LATENCY} -Xsoptimize=latency")
# The -reuse-exe flag enables rapid recompilation of host-only code changes.
# See C++SYCL_FPGA/GettingStarted/fast_recompile for details.
